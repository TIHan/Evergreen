#target "dotnet: net10.0"

#load "*.oly"

#reference "../Game/Server/Game.Server.olyx"
#reference "../Game/Client/Game.Client.olyx"
#reference "../Game/Shared/*.olyx"

#reference "Steamworks.NET.dll"
#copy "steam_api64.dll"
#copy "steam_appid.txt"

open System
open System.IO
open System.Runtime.Intrinsics
open Game.Resources // Resources
open Game.Shared // GameEnv
open Evergreen.Graphics.Rendering // RenderingOptions/Rendering
open static Game.Client.Rendering.Module // RenderEnv
open Evergreen.Utilities

open System.Numerics
open Evergreen.Database
open static Game.Shared.Types
open static Game.Shared.Components
open Game.Server
open Game.Graphics
open Steamworks

BenchmarkMap(resx: Resources, genv: GameEnv): Stream =
    let world = ServerWorld(genv)
    let db = world.UnlockedDatabase

    let mutable x = 0: float32
    let mutable y = 0: float32
    let mutable z = 0: float32
    resx.Images.ForEach(
        asset ->
            if (asset.Id.Value > 200)
                let mutable material = default: Material
                material.ImageId <- asset.Id
                material.Flags <- MaterialFlags.None

                let mutable transform = Transform()
                transform.Position <- vec3(x, y, 0)
                if (x >= 300: float32)
                    y <- y + 3
                    x <- 0
                else
                    x <- x + 3

                let meshRef = MeshReference(resx.RenderMeshes.FindId("cube.obj"))
                
                if (asset.Data[0].ImageKind != Evergreen.Graphics.Rendering.GpuImageKind.Frame && asset.Data[0].ImageKind != Evergreen.Graphics.Rendering.GpuImageKind.FrameView)
                    For(1, 
                        i -> 
                            let _ = db.Create(transform, material, meshRef)
                    )
    )

    let mutable material = default: Material
    material.ImageId <- resx.Images.FindId("Flats/Freedoom/aqf063.png")
    material.Flags <- MaterialFlags.None

    let mutable transform = Transform()
    transform.Position <- vec3(0, 0, -10)
    transform.Scale <- vec3(100, 100, 1)

    let meshRef = MeshReference(resx.RenderMeshes.FindId("cube.obj"))

    let mutable phys = StaticRigidBody()
    let mutable collider = MeshCollider()
    collider.MeshId <- resx.Meshes.FindId("cube.obj")

    let _ = db.Create(material, transform, meshRef, phys, collider)
    
    let ms = System.IO.MemoryStream()
    EntityDatabase.SerializeJson(db, ms)
    ms.Position <- 0
    ms

module Marker

BeginSteamAPI(): () =
    let origDir = Environment.CurrentDirectory
    let markerTy = DotNet.TypeOf<Marker>
    Environment.CurrentDirectory <- Path.GetDirectoryName(markerTy.Assembly.Location)
    let mutable isSteamRunning = SteamAPI.Init()
    printLine("Steam Running: " + isSteamRunning.ToString())
    Environment.CurrentDirectory <- origDir

EndSteamAPI(): () =
    SteamAPI.Shutdown()

main(args: string[]): () =
    if (args.Length != 1)
        fail("Invalid arguments")
    else
        Environment.CurrentDirectory <- args[0]

        let markerTy = DotNet.TypeOf<Marker>
        let file = File.OpenRead(markerTy.Assembly.Location)
        let (_, engineHash) = Hash.SHA256(file)
        file.Dispose()

        BeginSteamAPI()
        
        let options = RenderingOptions()
#if DEBUG
        options.IsBorderless <- true
        options.ValidationOptions <- Some(RenderingValidationOptions())
#end
        let r = Rendering(options)
        r.Window.Show()
        r.Gpu.RecreateSwapChainIfNecessary()

        // TODO: What do we do for server-only?
        let resx = Resources(r)

        let connectionKey = "oly" + engineHash
        let genv = GameEnv(connectionKey, engineHash, resx.Maps, resx.Meshes, resx.Sounds, resx.Pipelines, resx.Images)
        let renv = RenderEnv(r, resx.ShaderGlobals, resx.Frames, resx.Pipelines, resx.RenderMeshes, resx.Images)

        Tests.SerializeDeserializePlayer(genv)
        Tests.SerializeDeserializePlayer2(genv)
        Tests.SerializeDeserializePlayer3(genv)
        Tests.SerializeDeserializePlayer4(genv)
        Tests.DatabaseCopyInDifferenSync()
        Tests.NetworkCompressionShouldSucceed()

        resx.Maps.Register("benchmark.test",
            (_) -> return BenchmarkMap(resx, genv)
        )

        Console.SetOut(genv.ConsoleWriter)
        Game.Orchestration.Run(genv, renv)
        EndSteamAPI()