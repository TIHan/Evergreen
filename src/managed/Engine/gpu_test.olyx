#target "dotnet: net10.0"

#reference "../Window/Evergreen.Window.olyx"
#package "Spectre.Console,0.49.1"
#package "Spectre.Console.Cli,0.49.1"
#package "System.Drawing.Common,8.0.11"

#property "r2r" true

namespace GpuTest

open System
open System.Numerics
open System.IO
open System.Text.Json
open System.Text.Json.Serialization
open System.Runtime.Intrinsics
open Evergreen.Graphics.Rendering
open Evergreen.Utilities
open Spectre.Console
open Spectre.Console.Cli
open System.Drawing

open Evergreen.Graphics.Shader.Backend.Interop
open static Evergreen.Graphics.Shader.Backend.Interop.Methods

struct VertexPositionColor =
    Position: vec2 get, set
    TextureCoordinates: vec2 get, set
    Color: vec4 get, set

    new(position: vec2, texCoords: vec2, color: vec4) =
        this {
            Position = position
            TextureCoordinates = texCoords
            Color = color
        }

#[export]
struct TestData =
    public Value: float32 get, set = 0

#[export]
struct TestData2 =
    public Value: TestData get, set = TestData()

#[export]
struct TestData3 =
    public Value: TestData2 get, set = TestData2()

module Shader =

    public field mutable r: Rendering = unchecked default

    Run<T, TResult>(shaderKind: string, length: int32, input: mutable T[], spvBytes: mutable byte[]): mutable TResult[] where T: unmanaged where TResult: unmanaged =
        if (length <= 0)
            fail("Length is invalid")

        let output = zeroArray<TResult>(length)

        let isCompute =
            match (shaderKind)
            | "compute" => true
            | _ => false

        let defaultVertexCode = "#version 450
            
layout(location = 0) in vec2 Position;
layout(location = 1) in vec2 TexCoords;
layout(location = 2) in vec4 Color;

layout(location = 0) out vec2 fsin_TexCoords;
layout(location = 1) out vec4 fsin_Color;

void main()
{
    gl_Position = vec4(Position, 0, 1);
    fsin_TexCoords = TexCoords;
    fsin_Color = Color;
}"

        let defaultFragmentCode = "#version 450

layout(location = 0) in vec2 fsin_TexCoords;
layout(location = 1) in vec4 fsin_Color;
layout(location = 0) out vec4 fsout_Color;

void main()
{
    fsout_Color = fsin_Color;
}"

        if (r === unchecked default)
            let options = RenderingOptions()
            let validationOptions = RenderingValidationOptions()
            validationOptions.CanIgnoreWarnings <- true
            options.ValidationOptions <- Some(validationOptions)
            r <- Rendering(options)
            r.Window.Size <- Vector2(256, 256)

        let gpu = r.Gpu

        let layouts =
            match (shaderKind)
            | "vertex" =>
                [
                ]
            | "fragment" =>
                [
                ]
            | "compute" =>
                [
                    GpuDescriptorSetLayout(
                        [
                            GpuDescriptorSetLayoutBinding(GpuDescriptorKind.Storage, GpuDescriptorSetLayoutBindingFlags.None, GpuStageFlags.Compute)
                        ]
                    )
                    GpuDescriptorSetLayout(
                        [
                            GpuDescriptorSetLayoutBinding(GpuDescriptorKind.Storage, GpuDescriptorSetLayoutBindingFlags.None, GpuStageFlags.Compute)
                        ]
                    )
                ]
            | _ =>
                fail("Invalid shader kind")

        let shader: GpuShader = 
            match (shaderKind)
            | "vertex" =>
                let fragmentByteCode = RenderingHelpers.CompileShader(EgShaderKind.FRAGMENT, defaultFragmentCode)
                GpuShader.VertexFragment(GpuShaderCode(Unsafe.AsImmutable(spvBytes)), GpuShaderCode(fragmentByteCode))
            | "fragment" =>
                let vertexByteCode = RenderingHelpers.CompileShader(EgShaderKind.VERTEX, defaultVertexCode)
                GpuShader.VertexFragment(GpuShaderCode(vertexByteCode), GpuShaderCode(Unsafe.AsImmutable(spvBytes)))
            | _ =>
                GpuShader.Compute(GpuShaderCode(Unsafe.AsImmutable(spvBytes)))

        let mutable options = GpuPipelineOptions()
        options.PolygonMode <- GpuPolygonMode.Fill
        options.FrontFace <- GpuFrontFace.CounterClockwise
        options.CullMode <- GpuCullMode.Back
        options.Flags <- GpuPipelineFlags.FlipY | GpuPipelineFlags.DepthTest | GpuPipelineFlags.DepthWrite
        let testPipeline =
            GpuPipeline(
                "gpu_test",
                options,
                shader,
                layouts
            )

        match (shaderKind)
        | "vertex"
        | "fragment" =>
            testPipeline.SetVertexInput<VertexPositionColor>()
        | _ =>
            ()

        let quadVertices =
            [
                VertexPositionColor(vec2(-1, -1), vec2(0, 0), vec4(1, 0, 0, 1))
                VertexPositionColor(vec2(1, -1), vec2(1, 0), vec4(0, 1, 0, 1))
                VertexPositionColor(vec2(-1, 1), vec2(1, 1), vec4(0, 0, 1, 1))
                VertexPositionColor(vec2(1, 1), vec2(0, 1), vec4(1, 1, 0, 1))
            ]
    
        let quadIndices = [ 0: uint32; 1; 2;    3; 2; 1 ]

        let inputBuffer = GpuBuffer.Initialize(GpuBufferKind.Storage, GpuMemoryKind.Shared, length, i -> input[i])
        let outputBuffer = GpuBuffer.Initialize(GpuBufferKind.Storage, GpuMemoryKind.Shared, length, i -> output[i])
        let vertexBuffer = GpuBuffer.Initialize(GpuBufferKind.Vertex, GpuMemoryKind.Shared, quadVertices.Length, i -> quadVertices[i])
        let indexBuffer = GpuBuffer.Initialize(GpuBufferKind.Index, GpuMemoryKind.Shared, quadIndices.Length, i -> quadIndices[i])

        let cmds = GpuCommandList(10)
        GpuCommandList.Begin(cmds)
        match (shaderKind)
        | "compute" =>
            cmds.BindPipeline(testPipeline)
            cmds.BindDescriptorSet(0, inputBuffer.GetComputeDescriptorSet())
            cmds.BindDescriptorSet(1, outputBuffer.GetComputeDescriptorSet())
            cmds.Dispatch(input.Length, 1, 1)
            cmds.BeginSwapChainFrame()
            cmds.EndFrame()
        | _ =>
            cmds.BeginSwapChainFrame()
            cmds.BindPipeline(testPipeline)
            cmds.DrawIndexed(vertexBuffer, indexBuffer, indexBuffer.Length, 1)
            cmds.EndFrame()
        gpu.Execute(cmds)
        GpuCommandList.End(cmds)

        gpu.Wait()

        if (isCompute)
            gpu.Download(outputBuffer)

        let width = int32(r.Window.Size.X)
        let height = int32(r.Window.Size.Y)
        let imageData = gpu.DownloadSwapChainImageData(width, height)

        let result = outputBuffer.Array

        gpu.Free(testPipeline)
        gpu.Free(shader)
        gpu.Free(inputBuffer)
        gpu.Free(outputBuffer)
        gpu.Free(vertexBuffer)
        gpu.Free(indexBuffer)

        if (DotNet.TypeOf<TResult> == DotNet.TypeOf<int32> && !isCompute)
            Unsafe.As(map(imageData, x -> int32(x)))
        else if (isCompute)
            result
        else
            mutable []

#[export]
class Input =

    ShaderKind: string get, set = ""
    InputDataKind: string get, set = ""
    OutputDataKind: string get, set = ""

#[export]
class Input<T> =

    InputData: mutable T[] get, set = mutable []

module Helpers =

    RunShader<T>(shaderKind: string, length: int32, input: mutable T[], spvBytes: mutable byte[], options: JsonSerializerOptions, outputDataKind: string): string where T: unmanaged =
        match (outputDataKind)
        | "float" => JsonSerializer.Serialize(Shader.Run<T, float32>(shaderKind, length, input, spvBytes), options)
        | "int" => JsonSerializer.Serialize(Shader.Run<T, int32>(shaderKind, length, input, spvBytes), options)
        | "vec2" => JsonSerializer.Serialize(Shader.Run<T, Vector2>(shaderKind, length, input, spvBytes), options)
        | "vec3" => JsonSerializer.Serialize(Shader.Run<T, Vector3>(shaderKind, length, input, spvBytes), options)
        | "vec4" => JsonSerializer.Serialize(Shader.Run<T, Vector4>(shaderKind, length, input, spvBytes), options)
        | "mat4" => JsonSerializer.Serialize(Shader.Run<T, Matrix4x4>(shaderKind, length, input, spvBytes), options)
        | "TestData" => JsonSerializer.Serialize(Shader.Run<T, TestData>(shaderKind, length, input, spvBytes), options)
        | "TestData2" => JsonSerializer.Serialize(Shader.Run<T, TestData2>(shaderKind, length, input, spvBytes), options)
        | "TestData3" => JsonSerializer.Serialize(Shader.Run<T, TestData3>(shaderKind, length, input, spvBytes), options)
        | _ => fail("Invalid output data kind: " + outputDataKind)

#[export]
class ShaderCommand =
    inherits Command<Settings>

    class Settings =
        inherits CommandSettings

        #[CommandArgument(0, "[input]")]
        Input: string get, set = ""

        #[CommandArgument(1, "[shaderFile]")]
        ShaderFile: string get, set = ""

    overrides Execute(context: CommandContext, settings: Settings): int32 =
        let options = JsonSerializerOptions()
        options.IncludeFields <- true
        try
            let input = JsonSerializer.Deserialize<Input>(settings.Input, options)
            let path = settings.ShaderFile
            let shaderKind = input.ShaderKind
            let inputDataKind = input.InputDataKind
            let outputDataKind = input.OutputDataKind

            let output = 
                match (inputDataKind)
                | "float" => 
                    let input = JsonSerializer.Deserialize<Input<float32>>(settings.Input, options)
                    Helpers.RunShader(shaderKind, input.InputData.Length, input.InputData, File.ReadAllBytes(path), options, outputDataKind)
                | "int" =>
                    let input = JsonSerializer.Deserialize<Input<int32>>(settings.Input, options)
                    Helpers.RunShader(shaderKind, input.InputData.Length, input.InputData, File.ReadAllBytes(path), options, outputDataKind)
                | "vec2" =>
                    let input = JsonSerializer.Deserialize<Input<Vector2>>(settings.Input, options)
                    Helpers.RunShader(shaderKind, input.InputData.Length, input.InputData, File.ReadAllBytes(path), options, outputDataKind)
                | "vec3" =>
                    let input = JsonSerializer.Deserialize<Input<Vector3>>(settings.Input, options)
                    Helpers.RunShader(shaderKind, input.InputData.Length, input.InputData, File.ReadAllBytes(path), options, outputDataKind)
                | "vec4" =>
                    let input = JsonSerializer.Deserialize<Input<Vector4>>(settings.Input, options)
                    Helpers.RunShader(shaderKind, input.InputData.Length, input.InputData, File.ReadAllBytes(path), options, outputDataKind)
                | "mat4" =>
                    let input = JsonSerializer.Deserialize<Input<Matrix4x4>>(settings.Input, options)
                    Helpers.RunShader(shaderKind, input.InputData.Length, input.InputData, File.ReadAllBytes(path), options, outputDataKind)
                | "TestData" =>
                    let input = JsonSerializer.Deserialize<Input<TestData>>(settings.Input, options)
                    Helpers.RunShader(shaderKind, input.InputData.Length, input.InputData, File.ReadAllBytes(path), options, outputDataKind)
                | "TestData2" =>
                    let input = JsonSerializer.Deserialize<Input<TestData2>>(settings.Input, options)
                    Helpers.RunShader(shaderKind, input.InputData.Length, input.InputData, File.ReadAllBytes(path), options, outputDataKind)
                | "TestData3" =>
                    let input = JsonSerializer.Deserialize<Input<TestData3>>(settings.Input, options)
                    Helpers.RunShader(shaderKind, input.InputData.Length, input.InputData, File.ReadAllBytes(path), options, outputDataKind)
                | _ => fail("Invalid input data kind: " + inputDataKind)

            printLine(output)

        catch (ex: Exception) =>
            printLine("Error: " + ex.ToString().Replace("\n", "").Replace("\r", ""))

        0

module Program = 

    main(args: string[]): () =
        let app = CommandApp(null)
        app.Configure(
            config ->
                let _ = config.AddCommand<ShaderCommand>("shader")
        )

        while (true)
            let args = Console.ReadLine().Split(mutable [';'])
            let _ = app.Run(Cast(args))